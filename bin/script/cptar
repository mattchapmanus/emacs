#!/bin/csh -f

if ( $#argv < 2 ) then
  echo "Usage: $0:t [-p] from-dir to-dir"
  echo "  -p  create to-dir"
  exit 1
endif

set create = 0
set from = ""
set to   = ""

foreach arg ( $* )
  if ( "$arg" == "-p" ) then
    set create = 1
  else if ( "$from" == "" ) then
    set from = $arg
  else if ( "$to" == "" ) then
    set to = $arg
  else
    echo confused at $arg
    exit 1
  endif
end

if ( ! -d "$from" ) then
  echo "can't find $from"
  exit 1
endif

if ( "$to" == "." ) then
  set to = `pwd`
endif

if ( $create && "$to" != "" ) then
  mkdir -p "$to"
  if ($status != 0) then
    echo "can't create $to"
    exit 1
  endif
endif

cd $from

if ($status != 0) then
  echo "can't find $from"
  exit 1
endif

if ( ! -d $to ) then
  echo "can't find $to from $from"
  exit 1
endif

tar cf - . | (cd $to; tar xfBp -)

cd $to

if ($status != 0) then
  echo "can't cd to $to"
  exit 1
endif

exit 0
